#!/usr/bin/perl
use strict;
use warnings;

use Getopt::Long;
use HTTP::Tiny;
use Mail::DMARC;
use Pod::Usage;

my $dryrun = 0;
my $random = 0;

GetOptions (
    'dryrun' => \$dryrun,
    'help'   => \my $help,
    'random' => \$random,
    'config-file=s' => \my $config_file,
);

pod2usage if $help;

if ( $random ) {
    my $sleep_for = int(rand(60*60));
    sleep $sleep_for;
}

Mail::DMARC->new(
    (defined $config_file ? (config_file => $config_file) : ())
)->update_psl_file($dryrun);

__END__

=pod

=head1 NAME

dmarc_update_public_suffix_list: command line tool to download updated public suffix list

=head1 SYNOPSIS

  dmarc_update_public_suffix_list [ --option=value ]

=head1 DESCRIPTION

Downloads a new Public Suffix List to the location specified by /etc/mail-dmarc.ini

The PSL is maintained by the Mozilla Foundation. It is updated a few times per
month, you are requested to download no more than once per day.

The URL of the file is https://publicsuffix.org/list/effective_tld_names.dat
More details can be found at https://publicsuffix.org/

=head2 Options

  dmarc_update_public_suffix_list [ --dryrun --help ]

    dryrun       - show what would be done without overwriting file
    random       - introduce a random delay to spread server load
                   intended for use when running from crontab
    config-file  - alternate config file path
    help         - print this syntax guide

=head1 EXAMPLES

To check that a new file can be downloaded without error but not download the file:

  dmarc_update_public_suffix_list --dryrun

To download a new Public Suffix List to the location specified my mail-dmarc.ini

  dmarc_update_public_suffix_list

=head1 AUTHORS

=over 4

=item *

Matt Simerson <msimerson@cpan.org>

=item *

Davide Migliavacca <shari@cpan.org>

=item *

Marc Bradshaw <marc@marcbradshaw.net>

=back

=cut
