#!/usr/bin/perl
# VERSION
use strict;
use warnings;

use Getopt::Long;
use HTTP::Tiny;
use Mail::DMARC;
use Pod::Usage;

my $dryrun = 0;

GetOptions (
    'dryrun' => \$dryrun,
    'help'   => \my $help,
);

pod2usage if $help;

my $dmarc = Mail::DMARC->new();
my $psl_file = $dmarc->find_psl_file();

die "No Public Suffix List file found\n"                  if ( ! $psl_file );
die "Public suffix list file $psl_file not found\n"       if ( ! -f $psl_file );
die "Cannot write to Public Suffix List file $psl_file\n" if ( ! -w $psl_file );

my $url = 'https://publicsuffix.org/list/effective_tld_names.dat';
my $response = HTTP::Tiny->new->get( $url );
my $content = $response->{'content'};
if ( !$response->{'success'} ) {
    my $status  = $response->{'status'};
    die "HTTP Request for Public Suffix List file failed with error $status ($content)\n";
}

if ( ! $dryrun ) {
    open my $file, '>', $psl_file || die "Could not write to Public Suffix List file $psl_file\n";
    print $file $content;
    close $file;
    print "Public Suffix List file $psl_file updated\n";
}
else {
    print "Public Suffix List file $psl_file not updated (dryrun)\n";
}

# PODNAME: dmarc_update_public_suffix_list
# ABSTRACT: command line tool to download updated public suffix list
__END__
sub {}

=head1 SYNOPSIS

  dmarc_update_public_suffix_list [ --option=value ]

=head2 Options

  dmarc_update_public_suffix_list [ --dryrun --help ]

    dryrun       - show what would be done without overwriting file
    help         - print this syntax guide

=head1 EXAMPLES

To check that a new file can be downloaded without error but not download the file:

  dmarc_update_public_suffix_list --dryrun

To doanload a new Public Suffix List to the location specified my mail-dmarc.ini

  dmarc_update_public_suffix_list

=head1 DESCRIPTION

Downloads a new Public Suffix List to the location specified by /etc/mail-dmarc.ini

This file is maintained by the Mozilla Foundation, it is updated a few times per
month, you are requested to download no more than once per day.

The URL of the file is https://publicsuffix.org/list/effective_tld_names.dat
More details can be found on the https://publicsuffix.org/ website.

=cut

